dnl Process this file with autoconf to produce a configure script.

AC_PREREQ(2.50)
AC_INIT(grig, 0.3.99.1, groundstation-developer@lists.sourceforge.net)
AM_INIT_AUTOMAKE
AM_MAINTAINER_MODE
AC_ISC_POSIX
AC_PROG_GCC_TRADITIONAL

dnl check for programs
AC_PROG_CC
AC_PROG_RANLIB
AM_PROG_CC_STDC

AC_CHECK_PROGS(RPMBUILD, rpmbuild, rpm)

dnl check for headers
AC_HEADER_STDC
AC_HEADER_DIRENT

AC_CHECK_HEADERS([sys/time.h unistd.h getopt.h])

if test "${ac_cv_c_compiler_gnu}" = "yes"; then
  CFLAGS="${CFLAGS} -Wall"
fi

pkg_modules="gtk+-2.0 >= 2.4.0 gthread-2.0 >= 2.4.0"
PKG_CHECK_MODULES(PACKAGE, [$pkg_modules])
AC_SUBST(PACKAGE_CFLAGS)
AC_SUBST(PACKAGE_LIBS)

dnl Add the languages which your application supports here.
ALL_LINGUAS=
AM_GNU_GETTEXT([external])
# autoheader templates for AM_GNU_GETTEXT checks.
AH_TEMPLATE([ENABLE_NLS], [])
AH_TEMPLATE([HAVE_CATGETS], [])
AH_TEMPLATE([HAVE_GETTEXT], [])
AH_TEMPLATE([HAVE_LC_MESSAGES], [])
AH_TEMPLATE([HAVE_STPCPY], [])

dnl Set PACKAGE_LOCALE_DIR
if test "x${prefix}" = "xNONE"; then
  AC_DEFINE_UNQUOTED(PACKAGE_LOCALE_DIR, "${ac_default_prefix}/share/locale")
else
  AC_DEFINE_UNQUOTED(PACKAGE_LOCALE_DIR, "${prefix}/share/locale")
fi


dnl Set PACKAGE_DATA_DIR
if test "x${datadir}" = 'x${prefix}/share'; then
  if test "x${prefix}" = "xNONE"; then
    AC_DEFINE_UNQUOTED(PACKAGE_DATA_DIR, "${ac_default_prefix}/share/${PACKAGE}")
  else
    AC_DEFINE_UNQUOTED(PACKAGE_DATA_DIR, "${prefix}/share/${PACKAGE}")
  fi
else
  AC_DEFINE_UNQUOTED(PACKAGE_DATA_DIR, "${datadir}/${PACKAGE}")
fi

dnl Set PACKAGE_SOURCE_DIR
packagesrcdir=`cd $srcdir && pwd`
AC_DEFINE_UNQUOTED(PACKAGE_SOURCE_DIR, "${packagesrcdir}")

dnl First check through pkg-config, then fall back to standard way for old versions
PKG_CHECK_MODULES(HAMLIB, hamlib, [
  CFLAGS="$CFLAGS $HAMLIB_CFLAGS";
  LIBS="$LIBS $HAMLIB_LIBS";
  AC_DEFINE(WANT_HAMLIB, 1)
], [
  AC_CHECK_LIB(hamlib,main,,AC_MSG_ERROR([Hamradio control libraries not found...]))
  AC_CHECK_HEADERS(hamlib/rig.h,,AC_MSG_ERROR([Hamlib headers not found...]))
  AC_DEFINE(WANT_HAMLIB, 1)
])

dnl store hamlib version
AC_DEFINE_UNQUOTED(HAMLIB_VERSION,"`pkg-config --modversion hamlib`")
HAMLIB_V=`pkg-config --modversion hamlib`
GLIB_V=`pkg-config --modversion glib-2.0`
GTHR_V=`pkg-config --modversion gthread-2.0`
GDK_V=`pkg-config --modversion gdk-2.0`
GTK_V=`pkg-config --modversion gtk+-2.0`


AC_OUTPUT([ po/Makefile.in m4/Makefile 
Makefile
grig.1
grig.spec
src/Makefile
])

echo
echo SUMMARY:
echo
echo grig version....... : $VERSION
echo Hamlib version..... : $HAMLIB_V
echo Glib version....... : $GLIB_V
echo Gthread version.... : $GTHR_V
echo Gdk version........ : $GDK_V
echo Gtk+ version....... : $GTK_V
echo

